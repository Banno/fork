/*
 * Copyright 2015 Shazam Entertainment Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'application'
apply from: '../build-scripts/gradle-mvn-push.gradle'

mainClassName = 'com.shazam.fork.ForkCli'

sourceCompatibility = rootProject.sourceCompatibility
targetCompatibility = rootProject.sourceCompatibility

dependencies {
    compile(
            'com.beust:jcommander:1.30',
            'com.google.code.findbugs:jsr305:3.0.0',
            'com.android.tools.ddms:ddmlib:24.2.3',
            'com.google.code.gson:gson:2.3.1',
            'org.simpleframework:simple-xml:2.7.1',
            'com.google.guava:guava:18.0',
            'com.github.spullara.mustache.java:compiler:0.8.0',
            'org.slf4j:slf4j-log4j12:1.7.12',
            'com.madgag:animated-gif-lib:1.2',
            'org.apache.commons:commons-lang3:3.4',
            'commons-io:commons-io:2.4'
    )

    compile('org.lesscss:lesscss:1.7.0.1.1') {
        exclude module: 'slf4j-simple'
    }

    testCompile(
            'org.hamcrest:hamcrest-all:1.3',
            'junit:junit-dep:4.11')
}

jar {
    manifest {
        attributes 'Implementation-Title': POM_NAME,
                'Implementation-Version': VERSION_NAME,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion,
                'Main-class': mainClassName
    }
}

run {
    systemProperties System.getProperties()

    if(project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}
